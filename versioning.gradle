/*
 * Copyright (c) 2015 Andrey “Limych” Khrolenok
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

def setVersionNameAndCode(version) {
	File verFile = file("$rootDir/version.properties")

	Properties props = new Properties()

	def inc = 0
	def suffix = "-SNAPSHOT"
	def runTasks = gradle.startParameter.taskNames
	if( 'assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks ){
		inc = 1
		suffix = ""
	}

	def vName = version
	def vPatch
	def vBuild
	def vCode

	if( verFile.canRead() ){
		props.load(new FileInputStream(verFile))

		vPatch = props['VERSION_PATCH'].toInteger()
//		vBuild = props['VERSION_BUILD'].toInteger()
		vCode = props['VERSION_CODE'].toInteger()

		if( vName != props['VERSION_NAME'] )
			vPatch = 0
	} else {
		verFile.createNewFile()

		vPatch = 0
		vBuild = 1
		vCode = 1
	}

	props['VERSION_NAME'] = vName.toString()
	props['VERSION_PATCH'] = (vPatch + inc).toString()
//	props['VERSION_BUILD'] = (vBuild + 1).toString()
	props['VERSION_CODE'] = (vCode + inc).toString()

	props.store(verFile.newWriter(), null)

	android.defaultConfig.versionName "${vName}.${vPatch}${suffix}"
	android.defaultConfig.versionCode vCode
}